contract DoggyChain =

  record dog =
    { chip_id: string,
      owner: address,
      name: string,
      breed: string,
      photo_url: string, 
      missing: bool }

  record state =
    { dogs      : map(string, dog) }

  entrypoint init() =
    { dogs = {} }
    
  private function myDogs'(all' : list((string, dog)), dest' : list(dog), key' : address) : list(dog) =
    switch(all')
      [] => dest'
      (_, dog)::tl =>
        if (dog.owner == key') myDogs'(tl, dog :: dest', key')
        else myDogs'(tl, dest', key')
  
  entrypoint myDogs() : list(dog) =
    myDogs'(Map.to_list(state.dogs), [], Call.caller)
    
  private function missingDogs'(all' : list((string, dog)), dest' : list(dog)) : list(dog) =
    switch(all')
      [] => dest'
      (_, dog)::tl =>
        if (dog.missing) missingDogs'(tl, dog :: dest')
        else missingDogs'(tl, dest')
        
  entrypoint missingDogs() : list(dog) = 
    missingDogs'(Map.to_list(state.dogs), [])
  
  entrypoint getDog(chip_id' : string) : dog =
  	switch(Map.lookup(chip_id', state.dogs))
	    None    => abort("There is no dog with this chip ID registered.")
	    Some(x) => x

  stateful entrypoint registerDog(chip_id': string, name': string, breed': string, photo_url': string, missing': bool) =
    if(Map.member(chip_id', state.dogs))
      abort("This dog is already registered")

    let dog = { owner = Call.caller, chip_id = chip_id', name = name', breed = breed', photo_url = photo_url', missing = missing' }
    put(state{ dogs[chip_id'] = dog })